plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jreleaser' version '1.18.0'
    id 'signing'
    id 'com.gradleup.shadow' version '8.3.6'
}

group = "com.grafana"
version = project.properties['grafanaFoundationSDKVersion']

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withJavadocJar()
    withSourcesJar()
}

shadowJar {
    // allows maven to read the artifact (by default, it's "plain")
    archiveClassifier.set('')
    relocate 'com.fasterxml.jackson', 'com.grafana.relocated.jackson'
}

allprojects {
    version = rootProject.version
    apply plugin: "java"
    repositories {
        mavenCentral()
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }

    test {
        useJUnitPlatform()
    }
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

tasks.build.dependsOn shadowJar
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Grafana Foundation SDK'
                description = 'A set of tools, types and libraries for building and manipulating Grafana objects.'
                url = 'https://github.com/grafana/grafana-foundation-sdk'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = "grafana"
                        name = "Grafana Labs"
                        email = "platform-cat@grafana.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/grafana/grafana-foundation-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/grafana/grafana-foundation-sdk.git'
                    url = 'https://github.com/grafana/grafana-foundation-sdk'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
  release {
    github {
      enabled = false
      skipRelease = true
      changelog {
        enabled = false
      }
    }
  }
  announce {
    active = 'NEVER'
  }
  upload {
    active = 'NEVER'
  }
  signing {
    active = 'ALWAYS'
    armored = true
  }
  deploy {
    maven {
      mavenCentral {
        mavenCentralRelease {
          active = 'RELEASE'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
        mavenCentralSnapshot {
          active = 'SNAPSHOT'
          url = 'https://central.sonatype.com/repository/maven-snapshots'
          snapshotSupported = true
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}

tasks.withType(org.jreleaser.gradle.plugin.tasks.JReleaserConfigTask).configureEach {
    dryrun = false
    gitRootSearch = true
    strict = false
}
